#! /usr/bin/env zsh

# modified version of default `skim-history-widget` to allow dynamic editing or
# execution of selected history line
# TODO: look at shellcheck results when switching shebang to bash (anything to
# fix in zsh land?)
# ref - https://github.com/junegunn/fzf/issues/477#issuecomment-444053054
local selected num
setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
selected=( $(fc -l 1 | eval "$(__skimcmd) +s --tac --no-multi -n2..,.. --tiebreak=index --expect=ctrl-e $SKIM_CTRL_R_OPTS -q ${(q)LBUFFER}") )
local ret=$?
if [ -n "$selected" ]; then
  local accept=0
  if [[ $selected[1] = ctrl-e ]]; then
    accept=1
    shift selected
  fi
  num=${selected[1]}
  if [ -n "$num" ]; then
    zle vi-fetch-history -n $num
    [[ $accept = 0 ]] && zle accept-line
  fi
fi
zle reset-prompt
return $ret
