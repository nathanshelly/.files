source "$DOTFILES/zsh/fpath.zsh"
source "$DOTFILES/zsh/keymap.zsh"
source "$DOTFILES/zsh/options.zsh"


# store tokens/keys in `secrets.zsh`, gitignored to avoid checking in
[ -f "$DOTFILES/zsh/secrets.zsh" ] && source "$DOTFILES/zsh/secrets.zsh"

# load functions written as basic scripts
autoload -Uz $DOTFILES/functions/*(:t)

# allow aliases to use defined functions
source "$DOTFILES/zsh/alias.zsh"

# load !path && !completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source "$file"
done

# start autocomplete
# TODO: revisit caching here?
# ref - https://htr3n.github.io/2018/07/faster-zsh/#optimising-shell-prompts
autoload -Uz compinit
compinit -C

# load completion files
for file in ${(M)config_files:#*/completion.zsh}
do
  source "$file"
done

unset config_files

# files defining multiple functions must be run to make those functions
# available
# performed after setting fpath to make functions available to execute
_execute_functions
