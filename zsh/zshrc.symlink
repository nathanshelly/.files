# path to dotfiles
export DOTFILES="$HOME/.files"

# define variable to store list of files to source
# note: -U ensures path does not contain duplicates
typeset -U config_files

# enable `extended_glob` to exclude directories (submodules, zsh) in match
# disable immediately after to respect options in `options.zsh`
# TODO: avoid `extended_glob` hack, exclude directories w/o `extended_glob` set
setopt extended_glob
config_files=($DOTFILES/**/*.zsh~*/zsh/*~*/submodules/*)
setopt noextended_glob

# zsh conveniently links path array and PATH env var (along with other pairs)
# to add to path, write `path=(<new-addition> $path)`
typeset -U path

source "$DOTFILES/zsh/path.zsh"

# load path files
for file in ${(M)config_files:#*/path.zsh}
do
  source "$file"
done

source "$DOTFILES/zsh/env_vars.zsh"

# make colors defined by spectrum available in other files
source "$DOTFILES/zsh/spectrum.zsh"
source "$DOTFILES/zsh/prompt.zsh"
source "$DOTFILES/zsh/config.zsh"
source "$DOTFILES/zsh/fpath.zsh"
source "$DOTFILES/zsh/keymap.zsh"
source "$DOTFILES/zsh/options.zsh"

# source antibody - static method
# loads static file that must be regenerated any time a new plugin is added
# regenerate by running the following (aliased to `antibundle` in alias.zsh):
#   `antibody bundle < "$DOTFILES/zsh/plugins.txt" > "$DOTFILES/zsh/plugins.sh"`
# refs:
#   - https://getantibody.github.io/#static-loading
#   - https://github.com/getantibody/antibody
source "$DOTFILES/zsh/plugins.sh"

# source plugins configuration after sourcing plugins themselves
source "$DOTFILES/zsh/plugins.zsh"

# store tokens/keys in `secrets.zsh`, gitignored to avoid checking in
[ -f "$DOTFILES/zsh/secrets.zsh" ] && source "$DOTFILES/zsh/secrets.zsh"

# load functions written as basic scripts
autoload -Uz $DOTFILES/functions/*(:t)

# allow aliases to use defined functions
source "$DOTFILES/zsh/alias.zsh"

# load !path && !completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source "$file"
done

# start autocomplete
autoload -Uz compinit

# use cache for compinit
# TODO: profile this to see how much of a difference it actually makes
# ref - https://blog.callstack.io/supercharge-your-terminal-with-zsh-8b369d689770
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null \
  || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != "$updated_at" ]; then
  compinit -i
else
  compinit -C -i
fi

# load completion files
for file in ${(M)config_files:#*/completion.zsh}
do
  source "$file"
done

unset config_files

# files defining multiple functions must be run to make those functions
# available
# performed after setting fpath to make functions available to execute
_execute_functions
